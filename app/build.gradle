apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'io.gitlab.arturbosch.detekt'

repositories {
    google()
    mavenCentral()
}

// This is primarily to be able to run unit and instrumented tests in GitHub workflows,
// since google-services.json is unavailable and will cause the GoogleServices task to fail
if (!project.file('google-services.json').exists()) {
    android.applicationVariants.all { variant ->
        def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
        googleTask.enabled = false
    }
}

Properties props = new Properties()
props.load(new FileInputStream("$project.rootDir/app/version.properties"))
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}

def getVersionCode = { ->
    int major = ext.majorVersion as Integer
    int minor = ext.minorVersion as Integer
    int patch = ext.patchVersion as Integer

    return major * 10000 + minor * 100 + patch
}

def getVersionName = { ->
    if (ext.buildNumber) {
        return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}.${ext.buildNumber}"
    }

    return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
}

android {
    namespace 'bisq.android'
    compileSdkVersion 32
    defaultConfig {
        versionCode getVersionCode()
        versionName getVersionName()
        applicationId "bisq.android"
        minSdkVersion 21
        targetSdkVersion 32
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest {
            java.srcDirs += "$projectDir/src/testCommon/java"
        }
        test {
            java.srcDirs += "$projectDir/src/testCommon/java"
        }
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

dependencies {
    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.20.0")

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.google.android.material:material:1.6.1'

    implementation 'com.google.zxing:core:3.4.1'

    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:21.0.0'
    implementation 'com.google.firebase:firebase-messaging:23.0.5'

    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    implementation 'androidx.room:room-ktx:2.4.2'
    implementation 'androidx.room:room-runtime:2.4.2'
    annotationProcessor 'androidx.room:room-compiler:2.4.2'
    kapt 'androidx.room:room-compiler:2.4.2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.multidex:multidex:2.0.1"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation 'org.powermock:powermock:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'

    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"
    androidTestImplementation "io.mockk:mockk-android:1.12.3"
    androidTestImplementation 'org.awaitility:awaitility-kotlin:4.2.0'

    androidTestUtil 'androidx.test:orchestrator:1.4.1'
}
