buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:12.1.0'
    }
}

plugins {
    id 'com.google.devtools.ksp' version '2.0.21-1.0.28' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.4'
    id 'io.snyk.gradle.plugin.snykplugin' version '0.5'
    id 'org.jetbrains.kotlin.android' version '2.0.21' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.21' apply false
}

repositories {
    mavenCentral()
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

allprojects { project ->
    dependencyLocking {
        lockAllConfigurations()
    }
    tasks.register('resolveAndLockAll') {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            configurations.findAll {
                // Add any custom filtering on the configurations to be resolved
                it.canBeResolved
            }.each { it.resolve() }
        }
    }
}

subprojects {
    tasks.withType(Test).configureEach {
        jvmArgs = jvmArgs + [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED'
        ]
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

snyk {
    severity = 'high'
    autoDownload = true
    autoUpdate = true
    arguments = '--all-sub-projects'
}
