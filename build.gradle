buildscript {
    ext.kotlin_version = '2.2.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.1'
        classpath 'com.google.gms:google-services:4.4.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:13.0.0'
    }
}

plugins {
    id 'com.google.devtools.ksp' version '2.2.0-2.0.2' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.8'
    id 'io.snyk.gradle.plugin.snykplugin' version '0.7.0'
    id 'org.jetbrains.kotlin.android' version '2.2.0' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '13.0.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.0' apply false
}

repositories {
    mavenCentral()
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

allprojects { project ->
    dependencyLocking {
        lockAllConfigurations()
    }
    tasks.register('resolveAndLockAll') {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            configurations.findAll {
                // Add any custom filtering on the configurations to be resolved
                it.canBeResolved
            }.each { it.resolve() }
        }
    }
}

subprojects {
    tasks.withType(Test).configureEach {
        jvmArgs = jvmArgs + [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED'
        ]
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

snyk {
    severity = 'high'
    autoDownload = true
    autoUpdate = true
    arguments = '--all-sub-projects'
}
