import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

apply plugin: 'com.android.application'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

repositories {
    google()
    mavenCentral()
}

Properties props = new Properties()
props.load(new FileInputStream("$project.rootDir/version.properties"))
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}

def getVersionCode = { ->
    int major = ext.majorVersion as Integer
    int minor = ext.minorVersion as Integer
    int patch = ext.patchVersion as Integer

    return major * 10000 + minor * 100 + patch
}

def getVersionName = { ->
    if (ext.buildNumber) {
        return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}.${ext.buildNumber}"
    }

    return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
}

android {
    namespace 'bisq.android'
    compileSdk 36

    defaultConfig {
        applicationId "com.joachimneumann.bisq"
        versionCode getVersionCode()
        versionName getVersionName()
        minSdkVersion 26
        targetSdkVersion 36
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }

    android.applicationVariants.all { variant ->
        // Rename APK output
        variant.outputs.all { output ->
            def flavor = variant.flavorName ? "${variant.flavorName}-" : ""
            def buildType = variant.buildType.name
            def versionName = variant.versionName

            if (output.outputFileName != null && output.outputFileName.endsWith('.apk')) {
                output.outputFileName = "bisq-notifications-${flavor}${buildType}-${versionName}.apk"
            } else {
                output.outputFileName = "bisq-notifications-${variant.name}.apk"
            }
        }

        // Rename AAB output
        def bundleTaskName = "bundle${variant.name.capitalize()}"
        tasks.named(bundleTaskName).configure {
            doLast {
                def aabDir = layout.buildDirectory.dir("outputs/bundle/${variant.name}").get().asFile
                def aabFile = file("${aabDir}/${project.name}-${variant.name}.aab")
                def newName = file("${aabDir}/bisq-notifications-${variant.name}-${variant.versionName}.aab")

                if (aabFile.exists()) {
                    aabFile.renameTo(newName)
                    println "Renamed AAB to: ${newName.name}"
                } else {
                    println "Expected AAB not found at: ${aabFile}"
                }
            }
        }

        // Must disable the GoogleServices task when running unit and instrumented tests in GitHub
        // workflows, since google-services.json is unavailable and will cause it to fail.
        if (!project.file('google-services.json').exists()) {
            tasks.configureEach { Task task ->
                if (task.name == "process${variant.name.capitalize()}GoogleServices") {
                    task.enabled = false
                }
            }
        }
    }

}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

dependencies {
    detektPlugins('io.gitlab.arturbosch.detekt:detekt-formatting:1.23.8')

    implementation 'androidx.core:core-ktx:1.16.0'

    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.google.zxing:core:3.5.3'

    implementation platform('com.google.firebase:firebase-bom:34.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:22.5.0'
    implementation 'com.google.firebase:firebase-messaging:25.0.0'

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    implementation 'androidx.room:room-ktx:2.7.2'
    implementation 'androidx.room:room-runtime:2.7.2'
    annotationProcessor 'androidx.room:room-compiler:2.7.2'
    ksp 'androidx.room:room-compiler:2.7.2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.2'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.9.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.multidex:multidex:2.0.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'io.mockk:mockk:1.14.5'
    testImplementation 'org.powermock:powermock:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation(project(':testCommon'))

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.assertj:assertj-core:3.27.3'
    androidTestImplementation 'androidx.test:core-ktx:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    androidTestImplementation 'org.awaitility:awaitility-kotlin:4.3.0'
    androidTestImplementation 'io.mockk:mockk-android:1.14.5'
    androidTestImplementation(project(':testCommon')) {
        // Prevent conflict with mockk-android
        exclude group: 'io.mockk', module: 'mockk'
    }
    androidTestImplementation 'org.objenesis:objenesis:3.4'

    androidTestUtil 'androidx.test:orchestrator:1.6.1'
}

def androidTestsReportsDirectory() {
    return "${layout.buildDirectory.get()}/reports/androidTests/connected/debug"
}

tasks.register('embedScreenshotsIntoReport') {
    group = "reporting"
    doFirst {
        def failureScreenshotsDirectory =
            new File(androidTestsReportsDirectory(), "screenshots/failures")

        if (!failureScreenshotsDirectory.exists()) {
            return
        }

        println "Embedding failure screenshots into report from ${failureScreenshotsDirectory}"

        failureScreenshotsDirectory.eachFile { failedTestClassDirectory ->
            def failedTestClassName = failedTestClassDirectory.name

            failedTestClassDirectory.eachFile { failedTestFile ->
                def failedTestName = failedTestFile.name
                def failedTestNameWithoutExtension =
                    failedTestName.take(failedTestName.lastIndexOf('.'))
                def failedTestClassJunitReportFile =
                    new File(androidTestsReportsDirectory(), "${failedTestClassName}.html")

                if (!failedTestClassJunitReportFile.exists()) {
                    println "Could not find JUnit report file for test class " +
                        "'${failedTestClassJunitReportFile}'"
                    return
                }

                def failedTestJunitReportContent = failedTestClassJunitReportFile.text

                def patternToFind = "<h3 class=\"failures\">" +
                    "${failedTestNameWithoutExtension}</h3>"
                def patternToReplace = "${patternToFind} <img src=\"screenshots/failures/" +
                    "${failedTestClassName}/${failedTestName}\" width =\"360\" /><br />"

                failedTestJunitReportContent = failedTestJunitReportContent.replaceAll(
                    patternToFind, patternToReplace)

                failedTestClassJunitReportFile.write(failedTestJunitReportContent)
            }
        }
    }
}

static def deviceScreenshotsDir() {
    return "/storage/emulated/0/Pictures/bisq"
}

tasks.register('clearScreenshotsFromDevice', Exec) {
    group = "reporting"
    ignoreExitValue = true

    def adbExecutableProvider = project.provider { android.getAdbExecutable().absolutePath }
    def deviceScreenshotsDirProvider = project.provider { deviceScreenshotsDir() }

    doFirst {
        commandLine = [
            adbExecutableProvider.get(),
            'shell', 'rm', '-rf',
            deviceScreenshotsDirProvider.get()
        ]
    }
}

tasks.register('fetchScreenshotsFromDevice', Exec) {
    group = "reporting"
    ignoreExitValue = true

    def adbExecutableProvider = project.provider { android.getAdbExecutable().absolutePath }
    def deviceScreenshotsDirProvider = project.provider { "${deviceScreenshotsDir()}/." }
    def androidTestsReportsDirectoryProvider = project.provider { androidTestsReportsDirectory() }

    doFirst {
        commandLine = [
            adbExecutableProvider.get(),
            'pull',
            deviceScreenshotsDirProvider.get(),
            androidTestsReportsDirectoryProvider.get()
        ]
    }
}

tasks.configureEach { task ->
    if (task.name == 'connectedDebugAndroidTest') {
        task.doFirst { 'clearScreenshotsFromDevice' }
        task.finalizedBy { 'fetchScreenshotsFromDevice' }
        task.finalizedBy { 'clearScreenshotsFromDevice' }
        task.finalizedBy { 'embedScreenshotsIntoReport' }
    }
}
