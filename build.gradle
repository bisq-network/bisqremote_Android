buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath 'com.google.gms:google-services:4.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.0.3"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "12.0.3"
    id "io.gitlab.arturbosch.detekt" version "1.23.4"
    id "io.snyk.gradle.plugin.snykplugin" version "0.5"
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
}

repositories {
    mavenCentral()
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

allprojects { project ->
    dependencyLocking {
        lockAllConfigurations()
    }
    tasks.register('resolveAndLockAll') {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            configurations.findAll {
                // Add any custom filtering on the configurations to be resolved
                it.canBeResolved
            }.each { it.resolve() }
        }
    }
}

subprojects {
    tasks.withType(Test).configureEach {
        jvmArgs = jvmArgs + [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED'
        ]
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

snyk {
    severity = 'high'
    autoDownload = true
    autoUpdate = true
    arguments = '--all-sub-projects'
}
